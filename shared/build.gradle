apply plugin: 'com.android.library' // Android library to use the Android SDK
apply plugin: 'kotlin-multiplatform' // Kotlin KMP plugin for the Kotlin/Native compiler
apply plugin: 'kotlinx-serialization'

android { // only require because we need to access the AndroidSDK in the shared code
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 15
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin { // define the targets
//    jvm()
//    js()

    targets { // either the iOS Arm device or iOS simulator using X64
        final def iOSTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64

        fromPreset(iOSTarget, 'ios') { // iOS framework named shared is created for iOS
            binaries {
                framework('shared')
            }
        }
        fromPreset(presets.android, 'android') // define Android target for shared code
    }

    sourceSets { // define dependencies for the shared code
        commonMain.dependencies {
            // for the common code
            api 'org.jetbrains.kotlin:kotlin-stdlib-common'
            // we use -core and -core-common library versions in commonMain
            implementation "io.ktor:ktor-client-core:$ktor_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"
        }
        androidMain.dependencies {
            // android common dependencies
            api 'org.jetbrains.kotlin:kotlin-stdlib'
            // androidMain dependencies are the Android versions of the library
            implementation "io.ktor:ktor-client-android:$ktor_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
        }
        iosMain.dependencies {
            // don't need to add the standard library, Kotlin/Native will include the standard library when compiling code for iOS

            // core-native version of coroutines and ios-ktor client
            implementation "io.ktor:ktor-client-ios:$ktor_version"
            implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$kotlinx_coroutines_version"
            implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"
        }
    }

}

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

task packForXCode(type: Sync) {
    // Sets a directory for the framework
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    // Determines the correct framework to build based on the selected target in the Xcode project, with the default of DEBUG
    final String mode = project.findProperty("XCODE_CONFIGURATION")?.toUpperCase() ?: 'DEBUG'
    final def framework = kotlin.targets.ios.binaries.getFramework("shared", mode) // SharedCode -> shared (framework is named shared instead of SharedCode fromPreset.binaries.framework), iOS -> ios (we are using lowercase, fromPreset)

    inputs.property "mode", mode
    dependsOn framework.linkTask

    // Copy the file from the build directory to the framework directory
    from { framework.outputFile.parentFile }
    into frameworkDir

    // Bash script named gradlew is created in the framework directory that Xcode call to build the shared framework
    // The script uses the version of the JDK that is embedded in Android Studio
    doLast {
        new File(frameworkDir, 'gradlew').with {
            text = "#!/bin/bash\n" +
                    "export 'JAVA_HOME=${System.getProperty("java.home")}'\n" +
                    "cd '${rootProject.rootDir}'\n" +
                    "./gradlew \$@\n"
            setExecutable(true)
        }
    }
}

// The share build task depends on the packForXCode task
tasks.build.dependsOn packForXCode
